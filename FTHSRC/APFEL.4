

400 VALUE XRES
400 VALUE YRES

101 VALUE TIEFE

FVARIABLE XMIN		-2E XMIN F!
FVARIABLE XMAX		 2E XMAX F!
FVARIABLE YMIN		-2E YMIN F!
FVARIABLE YMAX		 2E YMAX F!
FVARIABLE DX
FVARIABLE DY

FVARIABLE CX
FVARIABLE CY
FVARIABLE X
FVARIABLE Y

FVARIABLE SQRX
FVARIABLE SQRY



: SET-DX ( -- )
	XMAX F@  XMIN F@  F-  
	XRES S>D D>F F/  DX F! ;


: SET-DY ( -- )
	YMAX F@  YMIN F@  F-  
	YRES S>D D>F F/  DY F! ;


: APFEL-INIT ( -- )
	XRES YRES OPEN-GRAPHICS  DROP
	TO YRES		TO XRES
	SET-DX		SET-DY
	CLEAR-GRAPHICS
	;



: FIRST-STEP ( -- )
	CX F@ X F!
	CY F@ Y F!
	;


: ITERATION-SCHRITT ( -- )
	CX F@ SQRX F@ SQRY F@ F- F+
	CY F@ X F@ Y F@ 2E F* F* F+
	Y F!  X F! 
	;


: |Z| ( -- r*r )
	X F@ FDUP F*  FDUP SQRX F!
	Y F@ FDUP F*  FDUP SQRY F!
	F+
	;


: ITERATION-LOOP ( -- tiefe )
	0 
	BEGIN 
		1+ DUP TIEFE < |Z| 4E F< AND
	WHILE
		ITERATION-SCHRITT 
	REPEAT ;


: APFEL-LOOP ( -- tiefe )
	FIRST-STEP  ITERATION-LOOP ;



: APFEL-SPALTE ( i -- ) 
	YMIN F@ CY F!
	YRES 0 
	DO
		APFEL-LOOP  1 AND   
		IF  DUP I  SET-PIXEL  THEN
		DY F@ CY F@  F+  CY F!
	LOOP
	DROP
	;



: APFEL ( -- )
	APFEL-INIT
	XMIN F@ CX F!
	XRES 0 
	DO
		I APFEL-SPALTE
		UPDATE-GRAPHICS
		DX F@ CX F@  F+  CX F!
	LOOP
	CLOSE-GRAPHICS
	;


		








