\  5.09.93

		( ------------ VDI ------------- )

DECIMAL

: wordarray CREATE 2* ALLOT 	 \ <elements> --- <>
		DOES>  SWAP 2* + ;	 \ <index> --- <addr>

12	wordarray contrl
128 wordarray intin \ d.h. maximale Stringlaenge fuer v_gtext: 128 Bytes
128 wordarray intout
128 wordarray ptsin
128 wordarray ptsout

: ->		' >BODY >abs ,	;		  \ "name" --

CREATE vdi_pb		-> contrl	
			-> intin	
			-> ptsin
			-> intout
			-> ptsout	

header: (gemvdi)	\ ( vdi_pblock -- )
	HEX 221E7073 code, 4E424E75 code, reveal DECIMAL

\		move.l	(a6)+,d1 ; vdi_pblock laden
\		moveq	#115,d0
\		trap	#2
\		rts

\ : >intin ( -- )
\        -1	3 contrl w@ ( -- #intin )  1-
\        ?DO  I intin w!  -1 +LOOP ;

\ : >ptsin ( -- )			\  2.09.93
\        -1   1 contrl w@ ( -- #ptsin ) 2*  1-
\        ?DO  I ptsin w!  -1 +LOOP ;

: >intin ( -- )
        3 contrl w@ ( -- #intin )  DUP 1- SWAP 0 
        ?DO  DUP >R I - intin w! R> LOOP 
		DROP ;

: >ptsin ( -- )			\  2.09.93
        1 contrl w@ ( -- #ptsin ) 2* DUP 1- SWAP 0
        ?DO  DUP >R I - ptsin w!  R> LOOP 
		DROP ;

: >contrl ( opcode, ptsin, ptsout, intin, intout --  )
	4 contrl w!
	3 contrl w! ( intin)
	2 contrl w!
	1 contrl w! ( ptsin)
	0 contrl w! ;

: gemvdi ( <ptsin>, <intin>, opcode, ptsin, ptsout, intin, intout -- )
	>contrl
	>intin	>ptsin
	vdi_pb >abs  (gemvdi)  ;


\ -----------------------------------------------------------------
\		VDI-Binding nach R.Aumiller (32FORTH)
\ -----------------------------------------------------------------

: smove ( 0str nach --	)
	0 >R								\ ZAEHLER
	BEGIN	OVER R@ + C@	( 0str nach c )
			OVER OVER SWAP ( 0str nach c c nach ) R@ 2* + w!
			0<>
	WHILE	R> 1+ >R
	REPEAT	2DROP R> ;	\ -> ASSEMBLER!

: intout>pad ( adr anzahl -- pad )
	DUP 0 ?DO OVER I 2* + w@ PAD I + C! LOOP PAD + 0 SWAP C! DROP PAD ;


: 0.0.0.0	0 0 0 0 ;
: 0.0.1.1	0 0 1 1 ;
: 0.0		0 0 ;
: 0.1.2.0	0 1 2 0 ;
: 0.1.0.0	0 1 0 0 ;
: 1.1.0 	1 1 0 ;
: 1.0.0 	1 0 0 ;



\ ** vdi control funktions **	


DECIMAL 	\ S" AES.4" INCLUDED


\ Virtuel workstation
: v_opnvwk		( prevhandle ... -- handle )
	100 0 6 11 45 gemvdi 6 contrl w@ ;
: open_vwork	( graf_handle -- vdi_handle )
		graf_handle 6 contrl w! 2DROP 2DROP
		1 1 1 1 1 1 1 1 1 1 2  v_opnvwk ; \ 600*400 Schwarz-Weiss
: v_clsvwk ( -- )
		101 0.0.0.0 gemvdi ;

\ Real workstation
: v_opnwk ( <work_in_paras>, handle ... -- handle )
		graf_handle 6 contrl w! 2DROP 2DROP
		1 0 6 11 45 gemvdi 6 contrl w@ ;
: v_clswk ( -- )
		2 0.0.0.0 gemvdi ;

: v_clrwk ( -- )
		3 0.0.0.0 gemvdi ;

: v_updwk ( -- )
		4 0.0.0.0 gemvdi ;
: vst_load_fonts ( -- additional )
		0 119 0.0.1.1 gemvdi 0 intout w@ ;
: vst_unload_fonts ( -- )
		0 120 0.0 1 0 gemvdi ;
: vs_clip ( x1 y1 x2 y2 flag -- )
		129 2 0 1 0 gemvdi ;


\ ** vdi output funktions **
: v_pline (  <x.y-koord.> count -- )
	6 SWAP 0.0 0 gemvdi ;
: v_pmarker (  <x.y-koord.> count -- )
	7 SWAP 0.0 0 gemvdi ;
: v_gtext (  x y 0string -- )
	-rot 1 ptsin w! 0 ptsin w!
	0 intin smove
	>R 8 1 0 R>  0 >contrl
	vdi_pb >abs  (gemvdi) ;

: v_fillarea (	<x.y-koord.> count -- )
	9 SWAP 0.0 0 gemvdi ;
: v_contourfill (  x y index -- )
	103 1 0 1 0 gemvdi ;
: vr_recfl (  x1 y1 x2 y2 -- )
	114 2 0.0 0 gemvdi ;

\ ** gdp's ** 27.08.93zd

DECIMAL

: do_gdp	 5 contrl w!	>R >R >R >R  11  R> R> R> R> gemvdi ;

: v_bar (  x1 y1 x2 y2 -- )
	2 0.0 0 1 do_gdp ;
: v_circle (  x y rad -- )
	0.0 ROT 0 3 0.0 0 4 do_gdp ;
: v_arc (  x y rad aw ew -- )
	>R >R >R 0.0.0.0 R> 0 R> R> 4 0 2 0 2 do_gdp ;
: v_pieslice (	x y rad aw ew -- )
	>R >R >R 0.0.0.0 R> 0 R> R> 4 0 2 0 3 do_gdp ;
: v_ellarc (  x y xr yr aw ew -- )
	2 0 2 0 6 do_gdp ;
: v_ellpie (  x y xr yr aw ew -- )
	2 0 2 0 7 do_gdp ;
: v_ellipse (  x y xr yr -- )
	2 0.0 0 5 do_gdp ;
: v_rbox (	x1 y1 x2 y2 -- )
	2 0.0 0 8 do_gdp ;
: v_rfbox (  x1 y1 x2 y2 -- )
	2 0.0 0 9 do_gdp ;
: v_justified (  x y laenge word_sp char_sp string -- )
	2 intin  smove >R
	11 2 0 R> 2+ 0 >contrl
	1 intin w! 0 intin w!
	0	(  x y laenge 0 )
	( -1) 0  3 DO I ptsin w! -1 +LOOP
	10	5 contrl w!
	vdi_pb >abs  (gemvdi) ;

\ ** raster operationen

: vro_cpyfm ( x1s y1s x2s y2s x1d y1d x2d y2d mode mfbs mfbd -- )
	9 contrl ! 7 contrl !		109 4 0 1 0 gemvdi ;
: vrt_cpyfm ( x1s y1s x2s y2s x1d y1d x2d y2d mode farb1 farb0 mfbs mfbd -- )
	9 contrl ! 7 contrl !		121 4 0 3 0 gemvdi ;
: vr_trnfm ( mfbs mfbd -- )
	9 contrl ! 7 contrl !		110 0.0.0.0 gemvdi ;
: v_get_pixel ( x y -- pix col )
	105 1.0.0 2 gemvdi 0 intout w@ 1 intout w@ ;


\ ** vdi attribut funktions ** )	28.08.93

DECIMAL

: vswr_mode ( mode --  )
	  32 0.0.1.1 gemvdi ;
: vs_color ( index r g b -- )
	  14 0.0 4 0 gemvdi ;
: vsl_type ( style --  )
	  15 0.0.1.1 gemvdi ;
: vsl_udsty ( pattern -- )
	  113 0.0 1 0 gemvdi ;
: vsl_width ( width --	)
	  0 16 1.1.0 0 gemvdi ;
: vsl_color ( index --	)
	  17 0.0.1.1 gemvdi ;
: vsl_ends ( beg end -- )
	  108 0.0 2 0 gemvdi ;
: vsm_type ( symbol --	)
	  18 0.0.1.1 gemvdi ;
: vsm_height ( height --  )
	  0 SWAP 19 1.1.0 0 gemvdi ;
: vst_font ( font -- )
	  21 0.0.1.1 gemvdi ;
: vst_height ( hoehe -- )
	  0 SWAP 12 1 2 0.0 gemvdi ;
: vst_rotation ( winkel -- )
	  13 0.0.1.1 gemvdi ;
: vst_alignment ( hor ver -- )
	  39 0.0 2 2 gemvdi ;
: vsm_color ( color --	)
	  20 0.0.1.1 gemvdi ;
: vst_effects ( effect --  )
	  106 0.0.1.1 gemvdi  ;
: vsf_udpat ( daten ebenen -- )
	  >R 112 0.0 R> 16 * 0 gemvdi ;
: vst_color ( color -- )
	  22 0.0.1.1 gemvdi ;

\ ** vdi inquire functions	28.08.93

DECIMAL

: vqt_name (  element -- index str.adr )
	130 0.0 1 33 gemvdi  0 intout w@ 1 intout 32 intout>pad ;
: vqm_attributes (	-- )
	36 0 1 0 3 gemvdi ;
: vq_extnd (  extend -- )
	102 0 6 1 45 gemvdi ;
: vq_color (  index flag -- )
	26 0.0 2 4 gemvdi ;
: vql_attributes (	-- )
	35 0 1 0 5 gemvdi ;
: vqf_attributes (	-- )
	37 0.0 0 5 gemvdi ;
: vqt_attributes (	-- )
	38 0 2 0 6 gemvdi ;

\ ** vdi fill attribute ** )	28.08.93

DECIMAL

: vsf_interior ( style -- )
	23 0.0.1.1 gemvdi ;
: vsf_style ( index -- )
	24 0.0.1.1 gemvdi ;
: vsf_color ( color -- )
	25 0.0.1.1 gemvdi ;
: vsf_perimeter ( flag -- )
	104 0.0.1.1 gemvdi ;
: vst_point
	107 0 2 1 1 gemvdi ;

\ input funktionen	28.08.93

DECIMAL

: control(4) 4 contrl w@ ;

: vsin_mode ( dev_type mode -- )
	33 0.0 2 1 gemvdi ;
: vrq_locator ( x y -- x y term )
	28 1.1.0 1 gemvdi 0 ptsout w@ 1 ptsout w@ 0 intout w@ ;
: vsm_locator ( x y -- x y term coor key )
	vrq_locator 2 contrl w@ control(4) ;
: vrq_valuator ( valu_in -- valu_out term )
	29 0.0 1 2 gemvdi 0 intout w@ 1 intout w@ ;
: vsm_valuator ( valu_in -- valu_out term status )
	vrq_valuator control(4) ;
: vrq_choice ( ch_in -- ch_out )
	30 0.0.1.1 gemvdi 0 intout w@ ;
: vsm_choice ( -- ch_out status )
	30 0.0.0.0 gemvdi control(4) ;
: vsm_string ( x y max_length echo_m  -- string status )
	31 1 0 2 128 gemvdi 0 intout control(4) intout>pad control(4) ;
: vsc_form ( form.adr -- )
	0 intin 74 MOVE 111 0.0 37 0 >contrl vdi_pb >abs  (gemvdi) ;
: v_show_c ( reset -- )
	122 0.0 1 0 gemvdi ;
: v_hide_c ( -- )
	123 0.0.0.0  gemvdi ;
: vq_key_s ( -- status )
	128 0.0 0 1 gemvdi 0 intout w@ ;

\ escape funktionen  28.08.93

DECIMAL

: do_escape ( n -- ) 5 contrl w! 5 0.0.0.0 gemvdi ;
: v_exit_cur  ( -- ) 2	do_escape ;
: v_enter_cur ( -- ) 3	do_escape ;
: v_curhome   ( -- ) 8	do_escape ;
: v_hardcopy  ( -- ) 17 do_escape ;




